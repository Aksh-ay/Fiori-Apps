@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Balance Order Quantity Sales View'
@Metadata.ignorePropagatedAnnotations: true
@VDM.viewType: #CONSUMPTION
@OData.publish: true
@Analytics.dataCategory: #CUBE
@ObjectModel.representativeKey: 'ContractNo'
define view entity ZSD_BALANCE_ORDER_QUANTITY 
  with parameters
    p_year  : gjahr,
    p_month : monat
  as select from    vbak                                                                               as _ContractHeader
    inner join      vbap                                                                               as _ContractItem          on _ContractItem.vbeln = _ContractHeader.vbeln

  /* Resolve Sales Orders created from this Contract */
    left outer join vbak                                                                               as _SalesOrderHeader      on  _SalesOrderHeader.vgbel = _ContractItem.vbeln
                                                                                                                                 and _SalesOrderHeader.vbtyp = 'C'

    left outer join vbap                                                                               as _SalesOrderItem        on _SalesOrderItem.vbeln = _SalesOrderHeader.vbeln

    left outer join ZSD_I_BILL_PRICING_AGG( p_year:$parameters.p_year, p_month: $parameters.p_month  ) as _BillingPricing        on  _BillingPricing.SalesOrderNo = _SalesOrderItem.vbeln
                                                                                                                                 and _BillingPricing.SalesItemNo  = _SalesOrderItem.posnr
  /* Delivery Info at Sales Order Item level */
    left outer join lips                                                                               as _DeliveryItem          on  _DeliveryItem.vgbel = _SalesOrderItem.vbeln
                                                                                                                                 and _DeliveryItem.vgpos = _SalesOrderItem.posnr

    left outer join ZSD_I_SALES_PARTNER                                                                as _SalesPartner          on _SalesPartner.ContractNo = _ContractItem.vbeln

    left outer join adrc                                                                               as _Address               on _Address.addrnumber = _SalesPartner.ShipToParty

    left outer join t023t                                                                              as _MaterialGroupText     on  _MaterialGroupText.matkl = _ContractItem.matkl
                                                                                                                                 and _MaterialGroupText.spras = $session.system_language

    left outer join tvsbt                                                                              as _ShippingConditionText on  _ShippingConditionText.vsbed = _ContractHeader.vsbed
                                                                                                                                 and _ShippingConditionText.spras = $session.system_language

    left outer join t001                                                                               as _CompanyText           on  _CompanyText.bukrs = _BillingPricing.CompanyCode
                                                                                                                                 and _CompanyText.spras = $session.system_language

    left outer join t052u                                                                              as _PaymentTermText       on  _PaymentTermText.zterm = _BillingPricing.PaymentTerm
                                                                                                                                 and _PaymentTermText.spras = $session.system_language
{
      /* ===============================
         Keys (CONTRACT ITEM)
         =============================== */
  key _ContractItem.vbeln                                    as ContractNo,
      _SalesOrderItem.vbeln                                  as SalesOrderNo,

      /* ===============================
         Contract DateTime info
         =============================== */
      substring( _ContractHeader.audat, 1, 4 )               as ContractYear,
      substring( _ContractHeader.audat, 5, 2 )               as ContractMonth,

      _ContractItem.matnr                                    as MaterialNo,
      _ContractItem.cuobj                                    as MaterialConfiguration,
      _ContractItem.matkl                                    as ProductCategory,
      _MaterialGroupText.wgbez                               as ProductCategorgyDesc,

      _SalesPartner.SoldToParty                              as SoldToParty,
      _Address.name1                                         as ShipToPartyName,
      
      @ObjectModel.virtualElement: true
      @ObjectModel.virtualElementCalculatedBy: 'ABAP:ZCL_SD_VE_SOLENGTH'
      cast( '' as atwrt )                                as SOLength,
      _BillingPricing.Currency                               as Currency,

      _BillingPricing.CompanyCode                            as CompanyCode,
      _CompanyText.butxt                                     as CompanyName,
      _BillingPricing.PaymentTerm                            as PaymentTerm,
      _PaymentTermText.text1                                 as PaymentTermText,
      _ContractHeader.vsbed                                  as ShippingType,
      _ShippingConditionText.vtext                           as ShippingTypeDesc,

      /* ===============================
        Unit Fields
        =============================== */
      _ContractItem.zieme                                    as ContractUnit,
      _DeliveryItem.vrkme                                    as DeliveryUnit,
      _BillingPricing.BillingUnit                            as BillingUnit,

      /* ===============================
        Quantity Fields
        =============================== */
      avg( _BillingPricing.BasicValue as abap.dec(15,2) )    as AvgBasicPrice,

      @Semantics.quantity.unitOfMeasure: 'ContractUnit'
      sum( cast( _ContractItem.zmeng as abap.dec(15,3) ) )   as ContractQty,

      @Semantics.quantity.unitOfMeasure: 'ContractUnit'
      sum(cast( _SalesOrderItem.kwmeng as abap.dec(15,3) ) ) as SalesOrderQty,

      @Semantics.quantity.unitOfMeasure: 'BillingUnit'
      sum( _BillingPricing.BilledQtyUptoMonth )              as TotalBilledQty,

      @Semantics.quantity.unitOfMeasure: 'DeliveryUnit'
      sum( _DeliveryItem.lfimg )                             as DeliveryQty,

      @Semantics.quantity.unitOfMeasure: 'ContractUnit'
      coalesce(
      sum( cast( _ContractItem.zmeng as abap.dec(15,3) ) ),
      cast( 0 as abap.dec(15,3) )
      )
      -
      coalesce(
      sum( cast( _BillingPricing.BilledQtyUptoMonth as abap.dec(15,3) ) ),
      cast( 0 as abap.dec(15,3) )
      )                                                      as BalanceBillQty
}
where
      _ContractHeader.vbtyp = 'G'
  and _ContractHeader.audat >= cast( concat( concat( $parameters.p_year, $parameters.p_month) , '01' ) as abap.dats )
  and _ContractHeader.audat < DATS_ADD_MONTHS(
                               cast( concat( concat( $parameters.p_year, $parameters.p_month) , '01' ) as abap.dats ),
                               1,
                               'INITIAL'
                              )
group by
  _ContractItem.vbeln,
  _SalesOrderItem.vbeln,
  _ContractHeader.audat,
  _ContractItem.matnr,
  _ContractItem.cuobj,
  _ContractItem.matkl,
  _MaterialGroupText.wgbez,
  _SalesPartner.SoldToParty,
  _Address.name1,
  _BillingPricing.CompanyCode,
  _CompanyText.butxt,
  _BillingPricing.PaymentTerm,
  _PaymentTermText.text1,
  _ContractHeader.vsbed,
  _ShippingConditionText.vtext,
  _BillingPricing.Currency,
  _ContractItem.zieme,
  _DeliveryItem.vrkme,
  _BillingPricing.BillingUnit;
